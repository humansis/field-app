apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'

// ****************** CONFIGURATION ************************ ////

def gitVersionCode = { boolean isFileName ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }

    def count = stdout.toString().trim().toInteger()

    if (!isFileName)  {
        // Ugly code that needs to be here because previous devs create high version code in google play
        count+=2001000
    }

    println count
    return count
}

// Major updates are non-compatible, meaning consumers can not upgrade without changing their software where applicable.
def VERSION_MAJOR = 3
// Minor updates are backward compatible, meaning consumers can upgrade freely. Main for non-breaking new functionality.
def VERSION_MINOR = 9
// Patch updates are interchangeable, meaning consumers can upgrade or downgrade freely. Mainly for bugfixing.
def VERSION_PATCH = 0

String APK_NAME = "field-app"
String VERSION_NAME = "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
String BUILD_NUMBER = "${gitVersionCode(true)}"
String VERSION_FULL_NAME_FILE = "${APK_NAME}-${VERSION_NAME}.${gitVersionCode(true)}"

repositories {
    google()
    mavenCentral()
}

android {

    compileSdkVersion 31

    ndkVersion "23.0.7599858"

    defaultConfig {
        applicationId "org.humansis.app.user"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode gitVersionCode(false)
        versionName VERSION_NAME
        setProperty("archivesBaseName", VERSION_FULL_NAME_FILE)

        resValue "string", "app_name", "Humansis Field"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "short", "BUILD_NUMBER", "$BUILD_NUMBER"

        buildConfigField "String", "MASTER_KEY", "\"$MASTER_KEY\""
        buildConfigField "String", "APP_ID", "\"$APP_ID\""

        buildConfigField "String", "PROD_API_URL", "\"$PROD_API_URL_MOBILE\""
        buildConfigField "String", "DEMO_API_URL", "\"$DEMO_API_URL\""
        buildConfigField "String", "STAGE_API_URL", "\"$STAGE_API_URL_MOBILE\""
        buildConfigField "String", "TEST_API_URL", "\"$TEST_API_URL\""
        buildConfigField "String", "DEV1_API_URL", "\"$DEV1_API_URL\""
        buildConfigField "String", "DEV2_API_URL", "\"$DEV2_API_URL\""
        buildConfigField "String", "DEV3_API_URL", "\"$DEV3_API_URL\""
        buildConfigField "String", "LOCAL_API_URL", "\"$LOCAL_API_URL\""

        buildConfigField "String", "DEMO_ACCOUNT", "\"$DEMO_ACCOUNT\""

        def currentAppTesterReleaseNotes
        if (project.hasProperty("appTesterReleaseNotes")) {
            currentAppTesterReleaseNotes = "$appTesterReleaseNotes"
        } else {
            currentAppTesterReleaseNotes = "Built from gradle"
        }

        firebaseAppDistribution {
            releaseNotes = currentAppTesterReleaseNotes
            groups="quanti-android-testers, pin-testers"
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    signingConfigs {
        release {
            storeFile file("$RELEASE_STORE_FILE_PATH")
            keyAlias "$RELEASE_STORE_KEY_ALIAS"
            storePassword "$RELEASE_STORE_PASSWORD"
            keyPassword "$RELEASE_STORE_KEY_PASSWORD"
        }
        debug {
            storeFile file(DEBUG_STORE_FILE_PATH)
            keyAlias DEBUG_STORE_KEY_ALIAS
            storePassword DEBUG_STORE_PASSWORD
            keyPassword DEBUG_STORE_KEY_PASSWORD
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    all {
        // exclude old hamcrests (from junit4 etc.) so we can use the newer one without conflicts
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    //firebase
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'

    // Artifactory
    def nfc_library_version = "2.1.8"
    implementation "cz.quanti.android:nfc:$nfc_library_version"

    // DI
    def dagger2_version = '2.23.2'
    implementation "com.google.dagger:dagger:$dagger2_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"

    // Database
    def room_version = "2.4.0-beta01"// TODO update to release version when possible
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "com.commonsware.cwac:saferoom.x:1.3.0"

    // ViewModels & Lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'

    // Bar code scanner // TODO DO NOT USE ANYMORE?
    implementation 'me.dm7.barcodescanner:zxing:1.9.8' //use older version until newer are available on mavenCentral

    // Worker
    def work_version = "2.7.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Spinner (actually usable one)
    implementation ('com.github.ganfra:material-spinner:2.0.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // Crash reporting - breaks down firebase and crashlytics
//    def acraVersion = '5.1.3'
//    implementation "ch.acra:acra-mail:$acraVersion"
//    implementation "ch.acra:acra-notification:$acraVersion"

    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation "android.arch.lifecycle:reactivestreams:1.0.0-beta2"

    // Kotlin logger
    def kotlin_logger_version = '2.2.6'
    implementation "com.github.Qase:KotlinLogger:$kotlin_logger_version"

    // ktlint
    ktlint "com.pinterest:ktlint:0.33.0"

    // JSON serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1"

    def test_version = "1.2.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.hamcrest:hamcrest:2.2'
    androidTestImplementation "androidx.test:runner:$test_version"
    androidTestImplementation "androidx.test:core-ktx:$test_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.1'
    androidTestImplementation "io.mockk:mockk-android:1.12.0"

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

